import uns.ftn.siit.sbnz.proj.sbnz.model.Razvoj;
import uns.ftn.siit.sbnz.proj.sbnz.model.Zemljiste;
import uns.ftn.siit.sbnz.proj.sbnz.model.Usev;
import uns.ftn.siit.sbnz.proj.sbnz.model.UsevPodaci;
import uns.ftn.siit.sbnz.proj.sbnz.model.Konfiguracija;
import uns.ftn.siit.sbnz.proj.sbnz.model.StanjeUseva;
import uns.ftn.siit.sbnz.proj.sbnz.model.VremenskaPrognoza;
import uns.ftn.siit.sbnz.proj.sbnz.model.Akcija;


import uns.ftn.siit.sbnz.proj.sbnz.model.Uslov;
import uns.ftn.siit.sbnz.proj.sbnz.model.UsloviAkcija;


import uns.ftn.siit.sbnz.proj.sbnz.medjuklase.PodKategorijaUseva;
import uns.ftn.siit.sbnz.proj.sbnz.medjuklase.Povrsina;
import uns.ftn.siit.sbnz.proj.sbnz.medjuklase.KategorijaUseva;
import uns.ftn.siit.sbnz.proj.sbnz.medjuklase.KategorijaNagiba;
import uns.ftn.siit.sbnz.proj.sbnz.medjuklase.KategorijaKlime;

import uns.ftn.siit.sbnz.proj.sbnz.model.enums.TipPovrsine;
import uns.ftn.siit.sbnz.proj.sbnz.model.enums.TipUseva;
import uns.ftn.siit.sbnz.proj.sbnz.model.enums.PodTipUseva;
import uns.ftn.siit.sbnz.proj.sbnz.model.enums.TipNagiba;
import uns.ftn.siit.sbnz.proj.sbnz.model.enums.TipKlime;
import uns.ftn.siit.sbnz.proj.sbnz.model.enums.TipPadavine;
import uns.ftn.siit.sbnz.proj.sbnz.service.AlertService;

dialect  "mvel"


declare Povrce
end

declare Voce
end

global AlertService alertService;


query "getRazvoj" 
	$razvoj: Razvoj()
end


rule "podtipovi-breskve"
	no-loop
	when
		$r : Razvoj( $usevi: ponudaUseva.ponude, ponudaUseva.ponude.size() > 0 )
        $usev: Usev( naziv == "Breskva") from $usevi
    then
    	$usev.setTipUseva(TipUseva.VOCNI);
    	$usev.setPodTipUseva(PodTipUseva.V_KOSTUNICAVI);
    	$r.dodajAkciju( new Akcija("Sadjenje", "Potrebno je posaditi breskvu"));
    	update($r);
end

rule "akcije-na-osnovu-uslova"
	no-loop
	when
		$r: Razvoj($unetaStanja: stanjaUseva)
        $s : StanjeUseva($usloviTogStanja: stanja) from $unetaStanja
		
		UsloviAkcija( $n: ponavljanje, $akcija: akcija, $uslovi: uslovi)

        
        Number($value: intValue >= $n) from accumulate(
    		Uslov(this memberOf $uslovi) from $usloviTogStanja,
        	count(1)
        )
    then
    	alertService.dodajAlert($r, $akcija);
    	update($r);
end

rule "aktiviraj-protivgradnu-mrezu"
	salience 100
	no-loop
	when
		$r: Razvoj()
		VremenskaPrognoza(tipPadavine == TipPadavine.GRAD) over window:time(5s)
	then
    	alertService.dodajAlert($r, new Akcija("Aktiviraj mrezu", "Potrebno je aktivirati protiv gradnu mrezu da bi se zastitli usevi"));
		update($r);
end


rule "prskanje-protiv-trulezi-i-jakog-vetra"
	salience 99
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 5) from accumulate(
			$vp: VremenskaPrognoza(
				tipPadavine == TipPadavine.KISA,
				kolicinaPadavina > 30
			) over window:length(7),
			count($vp)
		) and
		Number(intValue >= 3) from accumulate(
			$vp: VremenskaPrognoza(
				jacinaVetra > 5
			) over window:length(7),
			count($vp)
		) and not(
			exists (
				Akcija(
					naziv == "Prskanje"
				)
			)
		)
	then
		alertService.dodajAlert($r, new Akcija("Prskanje", "Potrebno je isprskati breskvu zbog jacanje same stabljike breskve"));
		update($r)
end


rule "zastita-od-trulezi-i-vetra"
	salience 98
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 5) from accumulate(
			$vp: VremenskaPrognoza(
				tipPadavine == TipPadavine.KISA,
				kolicinaPadavina > 30
			) over window:length(7),
			count($vp)
		) and
		Number(intValue >= 3) from accumulate(
			$vp: VremenskaPrognoza(
				jacinaVetra > 5
			) over window:length(7),
			count($vp)
		) and exists (
				Akcija(
					naziv == "Prskanje"
				)
			)
	then
		 alertService.dodajAlert($r,new Akcija("Rucna zastita",
		 "Potrebno je postaviti mehanicku ogradu kojom se ublazava jacina vetra i prokopati kanale za izlivanje kise"));
		 update($r)
end

rule "zastita-od-jakog-snega"
	salience 97
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 2) from accumulate(
			$vp: VremenskaPrognoza(
				tipPadavine == TipPadavine.SNEG,
				kolicinaPadavina > 5
			) over window:length(3),
			count($vp)
		) and
		Number(intValue >= 1) from accumulate(
			$vp: VremenskaPrognoza(
				jacinaVetra > 5
			) over window:length(3),
			count($vp)
		) and
		Number(intValue >=1) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina < 4
			) over window:length(1),
			count($vp)
		)
	then
		alertService.dodajAlert($r, new Akcija("Zastita od mraza", "Potrebno je breskvu zastiti prepartom"));
		update($r)
end



rule "zastita-od-blagog-snega"
	salience 96
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 2) from accumulate(
			$vp: VremenskaPrognoza(
				tipPadavine == TipPadavine.SNEG,
				kolicinaPadavina > 5
			) over window:length(3),
			count($vp)
		) and 
		Number(intValue >=1) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina < 4
			) over window:length(1),
			count($vp)
		)
	then
		alertService.dodajAlert($r, new Akcija("Zastita od mraza", "Potrebno je breskvu zastiti zagrevanjem zemlje"));
		update($r)
end

rule "obnova-nakon-vlaznog-mraza"
	salience 95
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 5) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina > 5,
				maximalnaTemperatura < 0
			) over window:length(5),
			count($vp)
		)
		and Number(intValue >=1) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina < 4
			) over window:length(1),
			count($vp)
		)
	then
		alertService.dodajAlert($r, new Akcija("Oporavak biljke", "Potrebno je breskvu zaliti da ne bi nastavila zamrzavanje"));
		update($r)
end

rule "obnova-nakon-susnog-mraza"
	salience 94
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 2) from accumulate(
			$vp: VremenskaPrognoza(
				maximalnaTemperatura < 0
			) over window:length(5),
			count($vp)
		)
		and Number(intValue >= 1) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina < 4
			) over window:length(1),
			count($vp)
		)
	then
		alertService.dodajAlert($r, new Akcija("Oporavak biljke", "Potrebno je oko biljaka zaloziti vatru"));
		update($r)
end

rule "prevencija-mraza"
	salience 93
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 2) from accumulate(
			$vp: VremenskaPrognoza(
				tipPadavine == TipPadavine.SNEG,
				kolicinaPadavina > 5
			) over window:length(5) from entry-point "predvidjena-prognoza",
			count($vp)
		) and Number(intValue >= 1) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina < 4
			) over window:length(1),
			count($vp)
		) and not(
			exists(
				Akcija(naziv == "Prevencija snega")
			)
		)
	then
		alertService.dodajAlert($r, new Akcija("Prevencija snega", "Potrebno je isprskati biljku hemijskim preparatom protiv mraza"));
		update($r)
end

rule "susni-period-prehrana-preparatom"
	salience 92
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 7) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina == 0,
				minimalnaTemperatura > 30
			) over window:length(7),
			count($vp)
		) and
		not( exists( Akcija(naziv=="Prskanje")))
	then
		alertService.dodajAlert($r, new Akcija("Prskanje", "Potrebno je isprskati biljku prehrabenim prepartom"));
		update($r)
end


rule "susni-period i pojava insekata"
	salience 91
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj($unetaStanja: stanjaUseva)
        $s : StanjeUseva($usloviTogStanja: stanja) from $unetaStanja
		
		Number($value: intValue >= 3) from accumulate(
    		Uslov(this == Uslov.PrisustvoLarvi 
    				|| this == Uslov.PrisustvoMuva 
    				|| this == Uslov.PrisustvoStonoga) from $usloviTogStanja,
        	count(1)
        ) and
		Number(intValue >= 7) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina == 0,
				minimalnaTemperatura > 30
			) over window:length(7),
			count($vp)
		)
		
	then
		alertService.dodajAlert($r, new Akcija("Zalivanje dodatno sulfatom", "Potrebno je odbraniti biljku uz pomoc sulfata u susnom periodu"));
		update($r)
end

rule "susni-period"
	salience 90
	no-loop
	activation-group "odbrana-useva"
	when
		$r: Razvoj()
		
		
		Number(intValue >= 7) from accumulate(
			$vp: VremenskaPrognoza(
				kolicinaPadavina == 0,
				minimalnaTemperatura > 30
			) over window:length(7),
			count($vp)
		) and
		exists( Akcija(naziv=="Prskanje"))
	then
		alertService.dodajAlert($r, new Akcija("Zalivanje", "Potrebno je zaliti biljku"));
		update($r)
end

	


